buildscript {
    ext {
        springBootVersion = '1.5.4.RELEASE'
        depMgmtPluginVersion = '1.0.3.RELEASE'
    }
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.9"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:${depMgmtPluginVersion}"
    }
}

allprojects {
    apply plugin: 'idea'


}

subprojects {
    apply plugin: 'com.ewerk.gradle.plugins.querydsl'

    apply plugin: 'java'
    sourceCompatibility = 1.8

    apply plugin: 'checkstyle'
    checkstyle {
        toolVersion = 6.7
        sourceSets = [sourceSets.main, sourceSets.test]
        checkstyleMain.configFile = file("${rootProject.projectDir}/config/checkstyle/checkstyle.xml")
    }

    apply plugin: 'io.spring.dependency-management'
    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "io.spring.platform:platform-bom:Brussels-SR3"
        }
    }

    repositories {
        mavenCentral()
    }

    querydsl {
        jpa = true
    }

    ideaModule.dependsOn compileQuerydsl

    idea {
        module {
            sourceDirs += file('src/querydsl/java')
        }
    }

    dependencies {
        compile('org.springframework.boot:spring-boot-starter-web')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}
